
#Область ПрограммныйИнтерфейс

Процедура Инициализировать(Форма, ГруппаВладелец) Экспорт
	
	СоздатьЭлементыФормыРедактора(Форма, ГруппаВладелец);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура СоздатьЭлементыФормыРедактора(Форма, ГруппаВладелец)
	
	// Программное создание необходимых реквизитов на форме
	
	// Реквизит для хранения текущего режима редактора
	РежимРедактора = Новый РеквизитФормы("РежимРедактора_Markdown",
		Новый ОписаниеТипов("Булево"));
		
	// Реквизит будет хранить исходный текст
	ТекстовоеСодержимое = Новый РеквизитФормы("ТекстовоеСодержимое_Markdown", 
		Новый ОписаниеТипов("Строка"), , , Истина);
		
	// Реквизит будет хранить преобразованный markdown в текст HTML
	СодержимоеHTML = Новый РеквизитФормы("СодержимоеHTML_Markdown", Новый ОписаниеТипов("Строка"));
		
	СоздаваемыеРеквизиты = Новый Массив;
	СоздаваемыеРеквизиты.Добавить(РежимРедактора);
	СоздаваемыеРеквизиты.Добавить(ТекстовоеСодержимое);
	СоздаваемыеРеквизиты.Добавить(СодержимоеHTML);
	
	Форма.ИзменитьРеквизиты(СоздаваемыеРеквизиты);
	
	// Установка значений новых реквизитов формы
	Форма["РежимРедактора_Markdown"] = Истина;
	
	// Программное создание команд
	СозданныеКоманды = Новый Структура;
	
	Команда = Форма.Команды.Добавить("ПереключитьРежимРедактора_Markdown");
	Команда.Действие        = "Подключаемый_ПереключитьРежимРедактораMarkdown";
	Команда.Картинка        = БиблиотекаКартинок.РежимПросмотра;
	Команда.Отображение     = ОтображениеКнопки.Картинка;
	Команда.Подсказка       = "Переключить редактор в режим редактирования или просмотра результата";
	Команда.СочетаниеКлавиш = Новый СочетаниеКлавиш(Клавиша.V, Истина, Ложь, Истина);
	
	СозданныеКоманды.Вставить("ПереключитьРежимРедактора", "ПереключитьРежимРедактора_Markdown");
	
	// Программное создание элементов формы
	Элементы = Форма.Элементы;
	
	// Общая группа, на которой будут размещены основные элементы управления текстом и отображением
	ГруппаРедакторТекста = Элементы.Добавить("ГруппаРедакторТекста_Markdown", 
		Тип("ГруппаФормы"), ГруппаВладелец);
		
	ГруппаРедакторТекста.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	ГруппаРедакторТекста.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	ГруппаРедакторТекста.ОтображатьЗаголовок = Ложь;
	
#Область ЗаполнениеОсновнойКоманднойПанели
	
	// Главная командная панель управления редактором
	ОсновнаяКоманднаяПанель = Элементы.Добавить("ОсновнаяКоманднаяПанель_Markdown", 
		Тип("ГруппаФормы"), ГруппаРедакторТекста);
		
	ОсновнаяКоманднаяПанель.Вид = ВидГруппыФормы.КоманднаяПанель;
	ОсновнаяКоманднаяПанель.РастягиватьПоГоризонтали = Истина;
	
	// Группа кнопок отображения кнопки переключения режимов "редактор" / "просмотр"
	ГруппаРежимРедактора = Элементы.Добавить("ГруппаРежимРедактора_Markdown", Тип("ГруппаФормы"),
		ОсновнаяКоманднаяПанель);
		
	ГруппаРежимРедактора.Вид = ВидГруппыФормы.ГруппаКнопок;
	
	// Кнопка переключения режима редактора
	КнопкаПереключенияРежима = Элементы.Добавить("КнопкаПереключенияРежимаРедактора_Markdown",
		Тип("КнопкаФормы"), ГруппаРежимРедактора);
		
	КнопкаПереключенияРежима.ИмяКоманды = СозданныеКоманды.ПереключитьРежимРедактора;
	
#КонецОбласти

#Область СозданиеРедактораТекста

	// Создание текстового поля для редактирования простого текста
	ПолеТекстовогоРедактора = Элементы.Добавить("ТекстовыйРедактор_Markdown", Тип("ПолеФормы"),
		ГруппаРедакторТекста);
		
	ПолеТекстовогоРедактора.ПутьКДанным = "ТекстовоеСодержимое_Markdown";
	ПолеТекстовогоРедактора.Вид = ВидПоляФормы.ПолеВвода;
	
	ПолеТекстовогоРедактора.АвтоМаксимальнаяШирина = Ложь;
	ПолеТекстовогоРедактора.АвтоМаксимальнаяВысота = Ложь;	
	ПолеТекстовогоРедактора.МногострочныйРежим = Истина;
	ПолеТекстовогоРедактора.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
	ПолеТекстовогоРедактора.РасширенноеРедактирование = Истина;
	ПолеТекстовогоРедактора.РастягиватьПоГоризонтали = Истина;
	ПолеТекстовогоРедактора.РастягиватьПоВертикали = Истина;	

#КонецОбласти

#Область СозданиеПросмотрщикаMarkdown

	// Создание поля HTML-документа для просмотра результата
	ПолеПросмотраHTML = Элементы.Добавить("ПросмотрРезультата_Markdown", Тип("ПолеФормы"), 
		ГруппаРедакторТекста);
	
	ПолеПросмотраHTML.ПутьКДанным = "СодержимоеHTML_Markdown";
	ПолеПросмотраHTML.Вид = ВидПоляФормы.ПолеHTMLДокумента;
	ПолеПросмотраHTML.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
	
	ПолеПросмотраHTML.АвтоМаксимальнаяШирина = Ложь;
	ПолеПросмотраHTML.АвтоМаксимальнаяВысота = Ложь;	
	ПолеПросмотраHTML.РастягиватьПоГоризонтали = Истина;
	ПолеПросмотраHTML.РастягиватьПоВертикали = Истина;	
	
	// TODO: Написать обработчик нажатия
	//ПолеПросмотраHTML.УстановитьДействие("ПриНажатии", "Подключаемый_ПриНажатииТекстHTML");

#КонецОбласти
	
КонецПроцедуры

#КонецОбласти